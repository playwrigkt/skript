buildscript {
  //main
  ext.kotlin_version = '1.2.30'
  ext.arrow_version = '0.7.1'
  ext.slf4j_version = '1.7.25'
  ext.jackson_version = '2.9.+'

  //test
  ext.kotlin_test_version="3.0.3"
  ext.junit_platform_version = "5.1.0"

  //build
  ext.dokka_version = "0.9.16"

  //implementation
  ext.kotlin_coroutine_version = '0.22.4'
  ext.vertxVersion = '3.4.2'
  ext.ktor_version = '0.9.1'
  ext.hikari_version = '2.7.1'
  ext.postgres_version = '42.1.4'

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'org.junit.platform.gradle.plugin'
  apply plugin: "jacoco"
  apply plugin: 'maven-publish'

  group = "playwrigkt.skript"
  version = "0.0.1"

  String moduleName = "skript${project.path.replace(":", "-")}"

  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  compileKotlin {
    kotlinOptions.jvmTarget= "1.8"
  }

  compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "io.arrow-kt:arrow-core:$arrow_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_platform_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_platform_version"
    testCompile "io.kotlintest:kotlintest-core:$kotlin_test_version"
    testCompile "io.kotlintest:kotlintest-assertions:$kotlin_test_version"
    testCompile "io.kotlintest:kotlintest-runner-junit5:$kotlin_test_version"
    testCompile "org.slf4j:slf4j-simple:$slf4j_version"
  }

  junitPlatform {

  }

  jacoco {
    applyTo junitPlatformTest
  }

  task junitPlatformCoverageReport(type:JacocoReport){
      executionData junitPlatformTest
      sourceSets sourceSets.main
      reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
      }
  }

  junitPlatformCoverageReport.dependsOn junitPlatformTest
  build.dependsOn junitPlatformCoverageReport

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
  }

  task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from dokka
  }

  task sourceJar(type: Jar) {
    baseName "$moduleName-sources"
    from sourceSets.main.allSource
    classifier "sources"
  }

  publishing {
      publications {
        mavenJava(MavenPublication) {
          groupId 'playwrigkt.skript'
          artifactId "$moduleName"

          from components.java
          artifact sourceJar
          artifact dokkaJar
        }
      }

      repositories {
          maven {
              url = uri("$buildDir/repository")
          }
      }
  }
}
