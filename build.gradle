buildscript {
  ext.kotlin_version = '1.2.30'
  ext.kotlin_coroutine_version = '0.22.4'
  ext.kotlin_test_version="3.0.3"
  ext.docker_plugin_version = '3.0.0'
  ext.vertxVersion = '3.4.2'
  ext.dokka_version = "0.9.16"
  ext.netty_version = "4.1.8.Final"
  ext.ktor_version = '0.9.1'
  ext.javalin_version = '1.5.0'
  ext.hikari_version = '2.7.1'
  ext.postgres_version = '42.1.4'
  ext.junit_platform_version = "5.1.0"
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0"
  }
}

subprojects {
  apply plugin:'java'
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'org.junit.platform.gradle.plugin'
  apply plugin: "jacoco"
  apply plugin: 'maven-publish'

  group = "playwrigkt.skript"
  version = "0.0.1"

  String moduleName = "skript${project.path.replace(":", "-")}"

  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  compileKotlin {
    kotlinOptions.jvmTarget= "1.8"
  }

  compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "org.funktionale:funktionale-all:1.2"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile("org.junit.jupiter:junit-jupiter-api:$junit_platform_version")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_platform_version")
    testCompile "io.kotlintest:kotlintest-core:$kotlin_test_version"
    testCompile "io.kotlintest:kotlintest-assertions:$kotlin_test_version"
    testCompile "io.kotlintest:kotlintest-runner-junit5:$kotlin_test_version"
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
  }

  junitPlatform {

  }

  jacoco {
    applyTo junitPlatformTest
  }

  task junitPlatformCoverageReport(type:JacocoReport){
      executionData junitPlatformTest
      sourceSets sourceSets.main
      reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
      }
  }

  junitPlatformCoverageReport.dependsOn junitPlatformTest
  build.dependsOn junitPlatformCoverageReport

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

//    linkMapping {
//      dir = "src/main/kotlin"
//      url = "https://github.com/dgoetsch/skript/tree/master/${project.name}/src/main/kotlin"
//      suffix = "#L"
//    }
  }

  task dokkaJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from dokka
  }

  task sourceJar(type: Jar) {
    baseName "$moduleName-sources"
    from sourceSets.main.allSource
    classifier "sources"
  }

  publishing {
      publications {
        mavenJava(MavenPublication) {
          groupId 'playwrigkt.skript'
          artifactId "$moduleName"

          from components.java
          artifact sourceJar
          artifact dokkaJar
        }
      }

      repositories {
          maven {
              url = uri("$buildDir/repository")
          }
      }
  }
}
